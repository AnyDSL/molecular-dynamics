/*fn print_particle_system(P: ParticleSystem) -> (){    
    let mut ic_start : [size_t * 3];
    let ic_end = P.nc();
    for d in map_range(0 as size_t, DIMENSION) {
        ic_start(d) = 0 as size_t;
    }

    for k in map_range(ic_start(2), ic_end(2)) {
        for j in map_range(ic_start(1), ic_end(1)) {
            for i in map_range(ic_start(0), ic_end(0)) {
                let ic = [i,j,k];
                if(*P.head(index(ic, P.nc())) != 0 as &mut ParticleList) {
                    print_string("Cell (");
                    print_i64(i as i64);
                    print_string(", ");
                    print_i64(j as i64);
                    print_string(", ");
                    print_i64(k as i64);
                    print_string("): ");
                    print_string(" (Index: ");
                    print_i64(index(ic, P.nc()) as i64);
                    print_string(" ) ");
                    for pl in map_list(*P.head(index(ic, P.nc()))) {
                        print_i64((*pl).data as i64);
                        print_char(' ');
                    }
                    print_char('\n');
                }
            }
        }
    }
}*/

fn insert_particle(node: &mut ParticleList, i: size_t, P: ParticleSystem) -> () {
    let p = get_particle_from_node(node);
    let kc = compute_cell_position(p, P);
    let root = P.head(index(kc, P.nc()));
    insert(root, node);
}

fn push_node(node: &mut ParticleList, P: ParticleSystem) -> () {
    let mut pos : [size_t * 3];
    for d in map_range(0 as size_t, DIMENSION) {
        pos(d) = P.nc()(d) / 2 as size_t;
    }
    let root = P.head(index(pos, P.nc()));
    insert(root, node);
}

fn compute_cell_position(p: Particle, P: ParticleSystem) -> [size_t * 3] {
    let mut kc : [size_t * 3];
    let coordinates_x = p.getCoordinates().get();
    let r_cut_inv = P.constants().r_cut_inv();
    let nc = P.nc();
    let coordinate_shift = P.coordinate_shift().get();
    for d in map_range(SIZE_T_0, DIMENSION) {
        kc(d) = math.floor((coordinates_x(d) + P.shadow_offset() + coordinate_shift(d)) * r_cut_inv) as size_t;
    }
    kc
}




fn map_particle_cells(start: [size_t * 3], end: [size_t * 3], P: ParticleSystem, f: fn([size_t * 3], &mut ParticleList) -> ()) -> () {
    for k in map_range(start(2), end(2)) {
        for j in map_range(start(1), end(1)) {
            for i in map_range(start(0), end(0)){
                let ic = [i,j,k];
                f(ic, *P.head(index(ic, P.nc())));
            }
        }
    }
}
fn parallel_map_particle_cells(start: [size_t * 3], end: [size_t * 3], P: ParticleSystem, numthreads: int, f: fn([size_t * 3], &mut ParticleList) -> ()) -> () {
    for k in parallel_map_range(start(2), end(2), numthreads) {
        for j in map_range(start(1), end(1)) {
            for i in map_range(start(0), end(0)){
                let ic = [i,j,k];
                f(ic, *P.head(index(ic, P.nc())));
            }
        }
    }
}

fn map_particle_system(start: [size_t * 3], end: [size_t * 3], P: ParticleSystem, f: fn([size_t * 3], &mut ParticleList) -> ()) -> () {
    for ic, head in map_particle_cells(start, end, P) {
        for pl in map_list(head) {
            f(ic, pl);
        }
    }
}

fn parallel_map_particle_system(start: [size_t * 3], end: [size_t * 3], P: ParticleSystem, numthreads: int, f: fn([size_t * 3], &mut ParticleList) -> ()) -> () {
    for ic, head in parallel_map_particle_cells(start, end, P, numthreads) {
        for pl in map_list(head) {
            f(ic, pl);
        }
    }
}

fn map_particle_system_with_ghost_layers(start: [size_t * 3], end: [size_t * 3], lower_boundary: [size_t * 3], upper_boundary :[size_t * 3], P: ParticleSystem, f: fn([size_t * 3], &mut ParticleList, bool) -> ()) -> () {
    for ic, head in map_particle_cells(start, end, P) {
        let mut write = true;
        for d in map_range(0 as size_t, DIMENSION) {
            if(ic(d) < lower_boundary(d) || ic(d) >= upper_boundary(d)) {
                write = false;
            }
        }
        for pl in map_list(head) {
            f(ic, pl, write);
        }
    }
}

